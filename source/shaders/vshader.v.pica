; Basic passthrough shader

; Uniforms
.fvec projection[4], modelView[4]
.bool byteColor

; Constants
.constf myconst(0.0, 1.0, -1.0, 0.00392156862)
.alias  zeros myconst.xxxx ; Vector full of zeros
.alias  ones  myconst.yyyy ; Vector full of ones
.alias  rgba8_to_float myconst.wwww

; Outputs
.out outpos position
.out outclr color
.out outtc0 texcoord0
.out outtc1 texcoord1

; Inputs (defined as aliases for convenience)
.alias inpos  v0
.alias inclr  v1
.alias intex  v2
.alias intex2 v3

.proc main
	; Force the w component of inpos to be 1.0
	mov r0.xyz, inpos
	mov r0.w,   ones

	; r1 = modelView * inpos
	dp4 r1.x, modelView[0], r0
	dp4 r1.y, modelView[1], r0
	dp4 r1.z, modelView[2], r0
	dp4 r1.w, modelView[3], r0

	; outpos = projection * r1
	dp4 outpos.x, projection[0], r1
	dp4 outpos.y, projection[1], r1
	dp4 outpos.z, projection[2], r1
	dp4 outpos.w, projection[3], r1

	mov outtc0, intex
	mov outtc1, intex2

	ifu byteColor
		mul outclr, rgba8_to_float, inclr
	.else
		mov outclr, inclr
	.end

	; We're finished
	end
.end